:MAIN
	# Default parameters:
	# 	ResolveTimeout = 0 (Undefined - Infinite)
	#	ConnectTimeout = 60000 (milliseconds - 60s)
	#	SendTimeout = 30000
	#   ReceiveTimeout = 30000
	#
	# -> https://learn.microsoft.com/es-es/windows/win32/winhttp/iwinhttprequest-settimeouts
	
	Set URL = "https://raw.githubusercontent.com/BlassGO/DinoIDE/main/images/Foods.json"

	#----------------------------------------------------------------------------------
	# Get an OBJECT from an JSON from a URL
	# ResponseJSON "URL" ResolveTimeout ConnectTimeout SendTimeout ReceiveTimeout 
	Set JSON = $(ResponseJSON URL)
	
	# Unlike the XML Object, a JSON Object is an exact representation of the tree and its keys.
	# It is a standard Object, and can be traversed directly
	Msg "FIRST - NAME" with JSON.foods.food[1].name
	Msg "FIRST - TYPE" with JSON.foods.food[1].type
	Msg "FIRST - INGREDIENTS" with JSON.foods.food[1].ingredients
	
	Msg "SECOND - NAME" with JSON.foods.food[2].name
	Msg "SECOND - TYPE" with JSON.foods.food[2].type
	Msg "SECOND - INGREDIENTS" with JSON.foods.food[2].ingredients
	
	#-----------------------------------ADVANCED--------------------------------------------
	# Use For or ForObj to loop through all existing elements
	For KEY FOOD in $(ResponseJSON URL).foods.food
	   Print "	Element:%KEY%"
	   For PROPERTY VALUE in FOOD
		   	Print "		%PROPERTY% -> %VALUE%"
	
	WaitConsole
	
