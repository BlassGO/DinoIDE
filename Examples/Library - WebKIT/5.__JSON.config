:MAIN
    # ----------------BUILD---------------------
    # Create an Array with 3 elements
    Set ARRAY = "First" "Second" "Third"
    
    # BuildJSON converts the Array to plain JSON text
    Msg "ARRAY -> JSON" with $(BuildJSON ARRAY)
	
	# Create an OBJECT with non-numeric associated keys
	Set OBJECT = $(Object "KEY" "Good morning" "KEY2" "Good afternoon")
	
	# BuildJSON converts the OBJECT to plain JSON text
	Msg "OBJECT -> JSON" with $(BuildJSON OBJECT)
	
	# Create a more complex OBJECT, with nested OBJECTS like an information tree
	Set OBJECT = $(Object "KEY" $(Object "SUBKEY" "Something") "KEY2" $(Object "SUBKEY" "Something") "KEY3" $(Array 1 2 3 4))
	
	# BuildJSON converts the entire tree recursively to plain JSON text
	Msg "OBJECT -> JSON" with $(BuildJSON OBJECT)
	
	
	
	# ----------------LOAD---------------------
	# JSON plain text can be obtained from any source.
	# For this demonstration a file will be used
	Delete "MyData.json"
	WriteFile "MyData.json" $(BuildJSON OBJECT)
	
	# Get JSON plaintext from source
	Set JSON = $(ReadFile "MyData.json")
	
	# LoadJSON converts plain text to an OBJECT
	Set DATA = $(LoadJSON JSON)
	
	# Since DATA is now a valid OBJECT, we can access its elements easily
	Msg "GET" with DATA.KEY.SUBKEY   # Something
	
	