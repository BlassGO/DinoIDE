:MAIN
	# Default parameters:
	# 	ResolveTimeout = 0 (Undefined - Infinite)
	#	ConnectTimeout = 60000 (milliseconds - 60s)
	#	SendTimeout = 30000
	#   ReceiveTimeout = 30000
	#
	# -> https://learn.microsoft.com/es-es/windows/win32/winhttp/iwinhttprequest-settimeouts
	
	Set URL = "https://raw.githubusercontent.com/BlassGO/DinoIDE/main/images/Foods.xml"

	#----------------------------------------------------------------------------------
	# Get an OBJECT from an XML from a URL
	# ResponseXML "URL" ResolveTimeout ConnectTimeout SendTimeout ReceiveTimeout 
	Set XML = $(ResponseXML URL)
	
	# Get the Root Node
	# using getElementsByTagName()
	# 	Item() -> Its Index starts from 0
	Set ROOT = XML.getElementsByTagName("foods").Item(0)
	
	# Get the first Node "Food"
	Set FOOD = ROOT.getElementsByTagName("food").Item(0)
	
	# Access Child Nodes using getElementsByTagName() too
	Print ">> Using getElementsByTagName() : Fist Node"
	Msg "FIRST - NAME" with FOOD.getElementsByTagName("name").Item(0).text
	Msg "FIRST - TYPE" with FOOD.getElementsByTagName("type").Item(0).text
	Msg "FIRST - INGREDIENTS" with FOOD.getElementsByTagName("ingredients").Item(0).text	
	Print "--------------------------------------------------------------"
	
	# Get the second Node "Food"
	Set FOOD = ROOT.getElementsByTagName("food").Item(1)

	# Access Child Nodes using selectSingleNode()
	Print ">> Using getElementsByTagName() + selectSingleNode() : Second Node"
	Msg "SECOND - NAME" with FOOD.selectSingleNode("name").text
	Msg "SECOND - TYPE" with FOOD.selectSingleNode("type").text
	Msg "SECOND - INGREDIENTS" with FOOD.selectSingleNode("ingredients").text	
	Print "--------------------------------------------------------------"
	
	# selectSingleNode() is quite convenient
	# Access to the elements of the first "Food" Node can be simplified to:
	Print ">> Using just selectSingleNode() : Fist Node"
	Msg "FIRST - NAME" with XML.selectSingleNode("//foods/food/name").text
	Msg "FIRST - TYPE" with XML.selectSingleNode("//foods/food/type").text
	Msg "FIRST - INGREDIENTS" with XML.selectSingleNode("//foods/food/ingredients").text
	Print "--------------------------------------------------------------"
	
	# It is also possible to specify a specific Node, by default it is always the first
	# 	Item() -> Its Index starts from 0
	#	selectSingleNode() -> When [INDEX] is specified, it starts from 1
	Print ">> Using just selectSingleNode() : Third Node"
	Msg "THIRD - NAME" with XML.selectSingleNode("//foods/food[3]/name").text
	Msg "THIRD - TYPE" with XML.selectSingleNode("//foods/food[3]/type").text
	Msg "THIRD - INGREDIENTS" with XML.selectSingleNode("//foods/food[3]/ingredients").text
	Print "--------------------------------------------------------------"

	# Using the ForObj loop it is possible to iterate over all existing child Nodes
	# 	-> For this, the most convenient thing may be to use the "childNodes" property.
	Print ">> Using the ForObj loop : Third Node"
	ForObj CHILD in XML.selectSingleNode("//foods/food[3]").childNodes
		Print "	 %CHILD.tagName% -> %CHILD.text%"
	Print "--------------------------------------------------------------"
	
	
	#-----------------------------------ADVANCED--------------------------------------------
	# Use ForObj to loop through all existing Nodes and their child Nodes
	Print ">> ADVANCED - ForObj loop : ALL Nodes"
	ForObj FOOD in $(ResponseXML URL).selectSingleNode("//foods").getElementsByTagName("food")
	   Print "	NODE:%Index%"
	   ForObj CHILD in FOOD.childNodes
		   	Print "		%CHILD.tagName% -> %CHILD.text%"
	
	WaitConsole
	
	
	
	