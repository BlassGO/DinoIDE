:MAIN
   # Switch may be more optimal than using multiple IFs, both in organization and speed
   # The "case" ones compare the value sent by the Switch and execute only the section that matches. There can be multiple values in the same CASE separated by commas.
   #      case DATA, DATA, DATA ...   
   # 
   # If no case matches, the "default" section will be executed (it should always be at the end)

   # Basic usage model
   Switch 3
      case 1
         Msg with "Its -> 1"
      case 2
      	 Msg with "Its -> 2"
      case 3
      	 Msg with "Its -> 3"
      default
         Msg with "Unknown"
   
   
   # Multiple DATA separated by a comma (,)
   # All whitespace is ignored unless it is enclosed in a group of double quotes
   Switch "Hello"
       case "First", 2, "Third"
          Msg with "Its  (First) OR (2) OR (Third)"
       case "Fourth", 5, "Hello"
          Msg with "Its  (Fourth) OR (5) OR (Hello)"

               	 
   # CASE evaluate each DATA allowing Operations or Reference to Variables
   # But it DO NOT SUPPORT Flexible Expressions like $( ) or % %
   Set X = 4
   Set Y = 2
   Switch X
      case 1
         Msg with "Is -> 1"
      case Y
      	 Msg with "Is -> %Y%"
      case Y + 2
         Set R = (Y+2)
      	 Msg with "Is -> %R%"
      default
         Msg with "Unknown"
         
   # CASE SENSITIVE
   # By default Switch comparisons are not case sensitive
   # Specifying "1" or "true" in the second Switch parameter will enable case sensitivity
   Set CASE_2 with "HELLO WORLD"
   Switch CASE_2  true
      case "hello world"
         Msg with "CASE 1"
      case "HELLO WORLD"
         Msg with "CASE 2"
         
   # LITERAL MODE
   # Specifying "2" in the second Switch parameter will enable literal mode
   # 	-> Faster comparisons because it does not have to evaluate Variables/Expressions
   # 	-> To include literal Commas / Spaces / Quotes / Escape characters, you must still use quote groupings.
   #    -> It is always case sensitive
   Switch CASE_2  2
      case hello world, "`n"
         Msg with "Its (hello world) OR LF"
      case 1,2,3,4,5,6,7,8,HELLO WORLD
       	 Msg with "Its (HELLO WORLD) OR 1...8"

   
   # Identify the type of Data by comparing the value returned by the TypeOf function
   # Literal mode "2" is ideal for this case
   Set VAR with $(Input "Enter a Number, Word or Date:")
   Switch $(TypeOf VAR) 2
	   case empty
		  Msg with "Is Empty!"
	   case integer, float
          Msg with "Is a Number!"
	   case space
	      Msg with "It is a space/line feed/tab/carriage return/vertical tab/formfeed"
	   case uppercase
		  Msg with "Uppercase characters!"
       case lowercase
		  Msg with "Lowercase characters!"
	   case alpha
	      Msg with "Alpha characters (Uppercase/Lowercase mixed)!"
	   case alnum
	      Msg with "Alphanumeric characters (alphabetic/numeric)!"
	   case time
	      Msg with "Date-time stamp, which can be all or just the leading part of the YYYYMMDDHH24MISS format."
       case string
          Msg with "Is a String!"
	   case array
          Msg with "Is an Array!"
       case object
          Msg with "Is an Object!"
       default
          Msg with "NO COINCIDENCE!"



