:MAIN
   # Switch may be more optimal than using multiple IFs, both in organization and speed
   # The "case" ones compare the value sent by the Switch and execute only the section that matches. There can be multiple values in the same CASE separated by commas.
   #      case DATA, DATA, DATA ...   
   # 
   # If no case matches, the "default" section will be executed (it should always be at the end)
   
   # Basic usage model
   Switch 3
      case 1
         Msg with "Is -> 1"
      case 2
      	 Msg with "Is -> 2"
      case 3
      	 Msg with "Is -> 3"
      default
         Msg with "Unknown"
      	 
   # The "CASE" are designed to make comparisons quickly. BUT:
   #	1. They support Arithmetic and concatenation Expressions. This includes references to Variables
   #	2. DO NOT SUPPORT Flexible Expressions like $( ) or % %
   Set X = 4
   Set Y = 2
   Switch X
      case 1
         Msg with "Is -> 1"
      case Y
      	 Msg with "Is -> %Y%"
      case Y + 2
      	 Msg with "Is -> %Y+2%"
      default
         Msg with "Unknown"
   
   # By default Switch comparisons are not case sensitive
   # Specifying "true" in the second Switch parameter will enable case sensitivity
   Set CASE_2 with "HELLO WORLD"
   Switch CASE_2 true
      case "hello world"
         Msg with "CASE 1"
      case "HELLO WORLD"
         Msg with "CASE 2"

   
   # Identify the type of Data by comparing the value returned by the TypeOf function
   Set VAR with $(Input "Enter a Number, Word or Date:")
   Switch $(TypeOf VAR)
	   case "empty"
		  Msg with "Is Empty!"
	   case "integer", "float"
          Msg with "Is a Number!"
	   case "space"
	      Msg with "It is a space/line feed/tab/carriage return/vertical tab/formfeed"
	   case "uppercase"
		  Msg with "Uppercase characters!"
       case "lowercase"
		  Msg with "Lowercase characters!"
	   case "alpha"
	      Msg with "Alpha characters (Uppercase/Lowercase mixed)!"
	   case "alnum"
	      Msg with "Alphanumeric characters (alphabetic/numeric)!"
	   case "time"
	      Msg with "Date-time stamp, which can be all or just the leading part of the YYYYMMDDHH24MISS format."
       case "string"
          Msg with "Is a String!"
	   case "array"
          Msg with "Is an Array!"
       case "object"
          Msg with "Is an Object!"
       default
          Msg with "NO COINCIDENCE!"



