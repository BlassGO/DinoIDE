:main
   # Simple loops
   # Do 1 ... N iterations
   # "Index" is a temporary variable existing during the execution of the loop, it counts the iterations performed
   Print "------------------------"
   For 100
      Print Index
   Print "------------------------"

   # Do N ... N2 iterations
   # For this case, the temporary variable "Index" will be associated with the value traversed from N
   For -2  2
      Print Index
   
   # It is possible to use Expressions as Numeric Indices for the loop
   Set X = -2
   For (X + 1)  0
      Print Index
   Print "------------------------"
   

   # Loops based on a set of sequential elements
   # "Index" also exists and can be related to the N element currently in use
   For X in 1 2 3 4
       Print X
   For LINE in "First line" "Second line" "Third line"
       Print LINE
   # Elements can also be expressions of any type. In addition, these are resolved as the loop progresses, you can take great advantage of
   For DATA in $(Input "Enter your name:") $(Input "Enter your last name:") $(Input "Enter your age:")
	   Msg with "You entered: %DATA%"
   Print "------------------------"
      

   # Loops based on elements of an Array/Object
   # "Index" also exists and can be related to the N element currently in use (In Arrays: Index = KEY)
   Set ARRAY with "A" "B" "C"
   For KEY VALUE in ARRAY
      Msg with "%KEY% = %VALUE%"
   
   # More than one Array / Object can be specified and all its elements will be traversed respectively
   Set OBJECT = $(Object "KEY" "Good morning" "KEY2" "Good afternoon")
   For KEY VALUE in OBJECT ARRAY
      Msg with "%KEY% = %VALUE%"

   
