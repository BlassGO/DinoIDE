:Table -> Table = [Tabla], Title = [Titulo], width = [ancho] , height = [altura], TAG
    Set N = Table[1]
    Set HEADER = With[1]
    If Width[1] && Height[1]
       # Sometimes, it is better to use a single (Expression) to use general string concatenation, rather than accumulating multiple expressions within the string.
       Set Dimen With ( "w" . Width[1] . " h" . Height[1] )
    If Title[1]
       Set Title = Title[1]
    Else
       Set Title = "Table%N%"
    If TAG[1]
       Set Dimen With ( Dimen . " g" . TAG[1] )
    Gui "new%N%: Destroy"
    Gui "new%N%: Default"
    Gui "new%N%: +AlwaysOnTop"
    Gui "new%N%: Add, ListView, % Dimen . ", " . HEADER  %"
    Set Columns With $(LV_GetCount "Column")
    If Columns
        # The HEADER should not be considered
        With.RemoveAt(1)
        # Native connectors cannot be used in a loop: For KEY VALUE in CONNECTOR, because the connector is intuited as part of the syntax        
        # It is mandatory to use the expansion pointer
        For VALUE in WITH*
           # New versions of DinoCode support pointer expansion even in expressions that produce an object
           LV_Add "" $(StrSplit VALUE "|")*
        # The FOR loop usually uses literal references to variables, so if you want to use its value as a parameter it is necessary to evaluate it in an expression
        For %Columns%
           LV_ModifyCol Index "AutoHdr"
    Gui "new%N%: Show, AutoSize, %Title%"

:main
   Message "HUH" with "Welcome to DinoCode!"
   If $(Question with "Do you want to continue?")
      Table 1 Title "huh" Width 200 Height 100 With "Column 1|Column 2|Column 3" "1|2|3" "4|5|6"
      Table 2 With "|Petecito" "1|Pwu" "2|Oleee"
      wait_window "huh"




