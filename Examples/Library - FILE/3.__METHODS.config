:MAIN
    # Create an OBJECT File
    Set file = $(New File with "test.txt" "UTF-8")   # With BOM

    # Basic properties
    # Ensures creating and cleaning file content
    Set file.content =
    
    # Check file existence
    if file.Exist()
        Msg "CHECK" with "The file exists!"
    Else
        Msg "CHECK" with "Cannot create file!"
        Abort
    
    # If you only want to add new content (without replacing the file)
    file.Write("First line`n")

    # If you want to add new lines
    # This adds the line break character to the end of the string
    file.WriteLine("Second line")

    # It is possible to change the line break character
    Set file.linebreak = "`r`n"

    # Now instead of using `n (Detected in the file) the manual definition `r`n will be used
    file.WriteLine("Third line")

    # Add content before a line that matches the pattern
    file.WriteBefore("Third ", "ADDED BEFORE Third")

    # Add content after a line that matches the pattern
    file.WriteAfter("Third ", "ADDED AFTER Third")
    
    # Get the total number of lines
    Set COUNT = file.Count()
    Msg "COUNT" with "There are %COUNT% lines"

    # Get the third line
    Msg "LINE" with file.Line(3)

    # Get the lines from the third to the fifth
    Msg "LINES" with file.Lines(3, 5)

    # Find the line that contains "Second"
    Msg "MATCHED" with file.Grep("Second")

    # Extract a section of lines from a line that contains the first pattern, to another that contains the second
    Msg "SECTION" with file.Grep("Second", "Third ")

    # By default the Grep method only obtains the first result, if you want all the matches use GrepAll
    Msg "ALL MATCHES" with file.GrepAll("F")

    # Replace all text matches with another, returns the number of replacements made
    Set NUMBER = file.Replace("line", "NEW TEXT")

    Msg "REPLACE" with "%NUMBER% replacements were made"

    # It is possible to limit the number of replacements
    # Only the first match will be replaced
    file.Replace("NEW TEXT", "Just Me", 1)
    
    # Remove(str, str2, limit)
    #	str = Find a line that contains this text
    #	str2 = Find a section that starts in the first str until a line that matches this second str2
    #	limit = Match limit (Number)
    #
    # Remove all or limited matches from the file
    file.Remove("NEW TEXT")

    # Basic properties
    Msg "CONTENT" with file.content

    # Clean the OBJECT
    Set file =
    
    
    