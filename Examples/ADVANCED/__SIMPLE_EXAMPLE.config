# DinoCode
# -> https://blassgo.github.io/DinoCode_Doc/

# This is a Simple function
:ReturnME
   Return "Hello World!"

# Main code
:MAIN
   # Variable assignment is direct
   Set NUMBER = 100
   Set STRING = "My string"
   
   # Variables can be referenced directly
   Message "STRING" with STRING
         
   # Any basic operation, both arithmetic and concatenation, can be placed within parentheses
   Message "ARITHMETIC" with (5 * 5 + NUMBER)
   Message "CONCAT" with (STRING . " concatenated with" . " my other string ")
   
   # Anything between percentages is considered an Expression: % Expression %
   # 	Allows greater flexibility, since it can be placed even within strings
   Message "Percent Expressions" with "The NUMBER variable has the value of: %NUMBER%"
   
   # To avoid the interpretation of Expressions within strings
   # You can use the default escape character: \
   Message "Escape" with "These percentages are literal: \% \%"
   
   # Escape character can be changed to any other for convenience
   Escape |
   Message "New Escape" with "|% |%"
   
   # You can call a Function using:
   #	$(Function Param1 Param2 ParamN ...)
   Message "Function Call" with $(ReturnME)
   
   # Allows greater flexibility, since it can be placed even within strings
   Message "HELLO" with "Function result: $(ReturnME)"
   
   
   
