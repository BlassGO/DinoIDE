# DinoCode
# -> https://blassgo.github.io/DinoCode_Doc/

# This is a Simple function
:ReturnME
   Return "Hello World!"

# Main code
:MAIN
   # Variable assignment is direct
   Set NUMBER = 100
   Set STRING = "My string"
   
   # Variables can be referenced directly
   Message "STRING" with STRING
   
   # To make Line breaks / Tabs / ... you can use the default Escape Character: \
   Message "PARAGRAPH" with "First line\n\tSecond line with TAB\nThird line"
      
   # To escape double quotes you must repeat the quote twice
   Message "QUOTES" with "Literal quotes: ""Quoted string"" "
         
   # Any basic operation, both arithmetic and concatenation, can be placed within parentheses
   Message "ARITHMETIC" with (5 * 5 + NUMBER)
   Message "CONCAT" with (STRING . " concatenated with" . " my other string ")
      
   # Anything between percentages is considered an Expression: % Expression %
   # 	Allows greater flexibility, since it can be placed even within strings
   Message "Percent Expressions" with "The NUMBER variable has the value of: %NUMBER%"
      
   # You can call a Function using:
   #	$(Function Param1 Param2 ParamN ...)
   Message "Function Call" with $(ReturnME)
   
   # Allows greater flexibility, since it can be placed even within strings
   Message "HELLO" with "Function result: $(ReturnME)"
   
   # To avoid the interpretation of Expressions within strings
   # You must repeat the character twice
   Message "Percentages" with "These percentages are literal: %% %%"
   Message "Dollar + Parenthesis" with "This does NOT call any function: $$(ReturnME)"
   

   
   
   
