# Actions with ADB/Fastboot(.exe) using AutoIMG Library

:MAIN
   # Change the default escape character, for greater convenience in file paths: from \ to `
   Escape `
   
   # Initialize AutoIMG dependencies
   AutoIMG_Set "adb = tools\adb.exe" "fastboot = tools\fastboot.exe" "general_log = Project.log"

   Msg with $(adb "version")
   
   Print "--------------------------------------------------------------"
   Print " Device INFO                                                  "
   Print "--------------------------------------------------------------"
   Print
   
   # Search for a device for 20 attempts
   Print ">> Waiting device"

   # There are several default prints in AutoIMG functions. To avoid them you can disable Print prior to use.
   Disable_print

   find_device 20

   # Deactivating Print prevents general printing, it is necessary to reactivate it
   Enable_print

   if exist_device
      Print ">> Device detected!"

   Switch device_mode
       case "booted"
          Msg with "The device is booted!"
       case "recovery"
          Msg with "The device is in Recovery mode!"
       case "fastboot"
          Msg with "The device is in Fastboot mode!"
       case "sideload"
          Msg with "The device is in Sideload mode!"
       default
          Msg with "The device was NOT detected!"
          Abort
   
   Print "   ---------------------------------------------           "
   Print "   Serial:       %serial%                                  "   
   Print "   Connection:   %device_connection%                       "   

   If (device_mode="booted") OR (device_mode="recovery")
       # Functions that use shell are only functional in modes where it is available
       Print "   Model:        $(shell "getprop ro.product.model")            "   
	   Print "   Version:      $(shell "getprop ro.build.version.release")    "   
	   Print "   State:        $(get_cmdline "androidboot.verifiedbootstate") "  
	   Print "   Slot:         $(get_slot)                                    "  
   Else If (device_mode="fastboot")
       # There are specific variables when the device was read in Fastboot mode
       if Unlocked
	      Print "   Unlocked:     TRUE            "   
       Else
          Print "   Unlocked:     FALSE            "   
       if FastbootD
	      Print "   FastbootD:    TRUE            "   
       Else
          Print "   FastbootD:    FALSE            "   
       Print "   Slot:         %current_slot%"   
       Print "   Anti:         %current_anti%"   

   Print "   ---------------------------------------------           "

   If $(Question "Reboot" with "Do you want to reboot your device?")
      Reboot
   
   WaitConsole
   Exit